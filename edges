#ifdef GL_FRAGMENT_PRECISION_HIGH
precision highp float;
#else
precision mediump float;
#endif

uniform vec2 resolution;
uniform vec2 cameraAddent;
uniform mat2 cameraOrientation;
uniform samplerExternalOES cameraBack;

uniform float time;

void make_kernel(inout vec3 n[9], vec2 uv){
	float w = 1.0 / resolution.x;
	float h = 1.0 / resolution.y;

	vec2 uvs[9];
	uvs[0] = uv + vec2(-w, -h);
	uvs[1] = uv + vec2(0, -h);
	uvs[2] = uv + vec2(w, -h);
	uvs[3] = uv + vec2(-w, 0);
	uvs[4] = uv;
	uvs[5] = uv + vec2(w, 0);
	uvs[6] = uv + vec2(-w, h);
	uvs[7] = uv + vec2(0, h);
	uvs[8] = uv + vec2(w, h);

	for (int i = 0; i < 9; ++i){
		if (uvs[i].x <= w || uvs[i].y <= h || uvs[i].x >= 1.0 - w || uvs[i].y >= 1.0 - h)
			n[i] = vec3(0.0, 0.0, 0.0);
		else
		n[i] = texture2D(cameraBack,cameraAddent+uvs[i]*cameraOrientation).rgb;
	}
}

void main(void) {
	float pixelSize = 4.0;
	vec2 uv = floor(gl_FragCoord.xy/pixelSize)*pixelSize / resolution.xy;

	vec3 n[9];
	make_kernel(n, uv);
	vec3 h_edge = n[2] + 2.0 * n[5] + n[8] - (n[0] + 2.0 * n[3] + n[6]);
	vec3 v_edge = n[0] + 2.0 * n[1] + n[2] - (n[6] + 2.0 * n[7] + n[8]);

	vec3 sobel = clamp(sqrt(h_edge * h_edge + v_edge * v_edge) * 4.0, 0.0, 1.0);

	vec3 color = vec3(
		uv,
		0.25 + 0.5 * sin(time));

	gl_FragColor = vec4(sobel.rgb * color, 1.0);
}